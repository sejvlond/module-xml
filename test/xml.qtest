#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

# make sure we have the right version of qore
%requires qore >= 0.8.12

%new-style
%require-types
%strict-args
%enable-all-warnings
%no-child-restrictions
%require-our

# test deprecated functions as well
%disable-warning deprecated

%requires QUnit

%requires Util
%requires xml

%exec-class XmlTest

class XmlTest inherits QUnit::Test {
    public {
        const Xsd = '<?xml version="1.0" encoding="utf-8"?>
<xsd:schema targetNamespace="http://qoretechnologies.com/test/namespace" xmlns:xsd="http://www.w3.org/2001/XMLSchema">
  <xsd:element name="TestElement">
    <xsd:complexType>
      <xsd:simpleContent>
        <xsd:extension base="xsd:string"/>
      </xsd:simpleContent>
    </xsd:complexType>
  </xsd:element>
</xsd:schema>
';

        const Dtd = '<!ELEMENT note (to,from,heading,body)>
<!ELEMENT to (#PCDATA)>
<!ELEMENT from (#PCDATA)>
<!ELEMENT heading (#PCDATA)>
<!ELEMENT body (#PCDATA)>';

        const InvalidDtd = "[] oops";

        const XmlStrDtd = '<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE note SYSTEM "Note.dtd">
<note>
<to>Person</to>
<from>Other</from>
<heading>Reminder</heading>
<body>My note</body>
</note> ';

        const DtdHash = (
            "note": (
                "to": "Person",
                "from": "Other",
                "heading": "Reminder",
                "body": "My note",
            ),
        );

        const Hash = (
            "test": 1,
            "gee": "philly",
            "marguile": 1.0392,
            "list": (1, 2, 3, ("four": 4), 5),
            "hash": ("howdy": 123, "partner": 456),
            "list^1": "test",
            "bool": True,
            "time": now(),
            "bool^1": False,
            #"emptyhash": {},
            #"emptylist": (),
            "key": "this & that",
            );

        const Str = "<?xml version=\"1.0\" encoding=\"utf-8\"?>
<file>
  <record id=\"1\"><name>test1</name></record>
  <record id=\"2\"><name>test2</name></record>
</file>
";

        const Rec = (
            "^attributes^": ("id": "1"),
            "name": "test1",
            );
    }

    constructor() : QUnit::Test("XmlTest", "1.0") {
        addTestCase("XmlTestCase", \xmlTestCase());
        addTestCase("DeprecatedXmlTestCase", \deprecatedXmlTestCase());
        addTestCase("FileSaxIteratorTestCase", \fileSaxIteratorTestCase());
        addTestCase("XmlCommentTestCase", \xmlCommentTestCase());
        addTestCase("getXmlValueTestCase", \getXmlValueTestCase());
        addTestCase("dtd test", \dtdTest());
        addTestCase("make_xmlWithHashOptsGeneral", \make_xmlWithHashOptsGeneralTestCase());
        addTestCase("make_xmlWithHashOptsArgumentDocVersion", \make_xmlWithHashOptsArgumentDocVersionTestCase());
        addTestCase("make_xmlWithHashOptsArgumentEncoding", \make_xmlWithHashOptsArgumentEncodingTestCase());
        addTestCase("make_xmlWithHashOptsArgumentFormatWithWhitespaces", \make_xmlWithHashOptsArgumentFormatWithWhitespacesTestCase());
        addTestCase("make_xmlWithHashOptsArgumentUseNumericRefs", \make_xmlWithHashOptsArgumentUseNumericRefsTestCase());
        addTestCase("make_xmlWithHashOptsArgumentDateFormat", \make_xmlWithHashOptsArgumentDateFormatTestCase());
        addTestCase("XmlDocConstructorFromHashTestCase", \XmlDocConstructorFromHashTestCase());
        addTestCase("XmlDocConstructorFromStringTestCase", \XmlDocConstructorFromStringTestCase());
        addTestCase("XmlDocValidateSchemaTestCase", \XmlDocValidateSchemaTestCase());
        set_return_value(main());
    }

    dtdTest() {
        hash h = parse_xml_with_dtd(XmlStrDtd, Dtd);
        assertEq(DtdHash, h);

        assertThrows("DTD-SYNTAX-ERROR", \parse_xml_with_dtd(), (XmlStrDtd, InvalidDtd));
        assertThrows("DTD-VALIDATION-ERROR", \parse_xml_with_dtd(), (Str, Dtd));

        {
            XmlDoc xd(XmlStrDtd);
            xd.validateDtd(Dtd);
            assertEq(True, True);
        }

        {
            XmlDoc xd(XmlStrDtd);
            assertThrows("DTD-SYNTAX-ERROR", \xd.validateDtd(), InvalidDtd);
        }

        {
            XmlDoc xd(Str);
            assertThrows("DTD-VALIDATION-ERROR", \xd.validateDtd(), Dtd);
        }
    }

    xmlTestCase() {
        hash o = Hash;
        hash mo = ("o": o);
        string str = make_xml("o", o);
        assertEq(True, mo.o == parse_xml(str, XPF_PRESERVE_ORDER).o, "first parse_xml()");
        str = make_xml("o", o, XGF_ADD_FORMATTING);
        assertEq(True, mo == parse_xml(str, XPF_PRESERVE_ORDER), "second parse_xml()");

        list params = (1, True, "string", NOTHING, o + ("emptylist": (), "emptyhash": hash()));
        str = make_xmlrpc_call("test.method", params, XGF_ADD_FORMATTING);
        hash result = ( "methodName" : "test.method", "params" : params );
        assertEq(result, parse_xmlrpc_call(str), "make_xmlrpc_call() and parse_xmlrpc_call() 1");
        str = make_xmlrpc_call("test.method", params);
        assertEq(result, parse_xmlrpc_call(str), "make_xmlrpc_call() and parse_xmlrpc_call() 2");
        str = make_xmlrpc_call("test.method", (True, o));
        result = ( "methodName" : "test.method","params" : (True, o) );
        assertEq(result, parse_xmlrpc_call(str), "make_xmlrpc_call() and parse_xmlrpc_call() 3");
        str = make_xmlrpc_call("test.method", (True, o), XGF_ADD_FORMATTING);
        assertEq(result, parse_xmlrpc_call(str), "make_xmlrpc_call() and parse_xmlrpc_call() 4");

        str = make_xmlrpc_response(o);
        assertEq(("params": o), parse_xmlrpc_response(str), "make_xmlrpc_response() and parse_xmlrpc_response() 1");
        str = make_xmlrpc_response(o, XGF_ADD_FORMATTING);
        assertEq(("params": o), parse_xmlrpc_response(str), "make_xmlrpc_response() and parse_xmlrpc_response() 2");
        str = make_xmlrpc_fault(100, "error");
        hash fr = ( "fault" : ( "faultCode" : 100, "faultString" : "error" ) );
        assertEq(fr, parse_xmlrpc_response(str), "make_xmlrpc_fault() and parse_xmlrpc_response() 1");
        str = make_xmlrpc_fault(100, "error", XGF_ADD_FORMATTING);
        assertEq(fr, parse_xmlrpc_response(str), "make_xmlrpc_fault() and parse_xmlrpc_response() 2");
        o = ( "xml" : (o + ( "^cdata^" : "this string contains special characters &<> etc" )) );
        assertEq(True, o == parse_xml(make_xml(o), XPF_PRESERVE_ORDER), "xml serialization with cdata");

        if (Option::HAVE_PARSEXMLWITHSCHEMA) {
            o = ( "ns:TestElement" : ( "^attributes^" : ( "xmlns:ns" : "http://qoretechnologies.com/test/namespace" ), "^value^" : "testing" ) );

            assertEq(o, parse_xml_with_schema(make_xml(o), Xsd), "parse_xml_with_schema()");
        }

        str = make_xml(mo);
        XmlDoc xd(str);
        assertEq(True, xd.toQore() == mo, "XmlDoc::constructor(<string>), XmlDoc::toQore()");
        assertEq(True, parse_xml(xd.toString(), XPF_PRESERVE_ORDER) == mo, "XmlDoc::toString()");
        XmlNode n = xd.evalXPath("//list[2]")[0];
        assertEq("2", n.getContent(), "XmlDoc::evalXPath()");
        assertEq("XML_ELEMENT_NODE", n.getElementTypeName(), "XmlNode::getElementTypeName()");
        n = xd.getRootElement().firstElementChild();
        assertEq("test", n.getName(), "XmlDoc::geRootElement(), XmlNode::firstElementChild(), XmlNode::getName()");
        n = xd.getRootElement().lastElementChild();
        assertEq("key", n.getName(), "XmlNode::lastElementChild()");
        assertEq("bool", n.previousElementSibling().getName(), "XmlNode::previousElementSibling()");
        assertEq(14, xd.getRootElement().childElementCount(), "XmlNode::childElementCount()");

        xd = new XmlDoc(mo);
        assertEq(True, xd.toQore() == mo, "XmlDoc::constructor(<hash>), XmlDoc::toQore()");

        XmlReader xr = new XmlReader(xd);
        # move to first element
        xr.read();
        assertEq(Xml::XML_NODE_TYPE_ELEMENT, xr.nodeType(), "XmlReader::read(), XmlReader::Type()");
        assertEq(True, xr.toQore() == mo.o, "XmlReader::toQoreData()");

        # #1326: module-xml: make_xml() is too strict for tag names, resulting in MAKE-XML-ERROR: tag: "_some_tag" is not a valid XML tag element name if tag starts with underscore
        hash underscore = ( "foo" : ("_underscore_test" : 1, ) );
        string underscoreResult = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n<foo><_underscore_test>1</_underscore_test></foo>\n";
        assertEq(underscoreResult, make_xml(underscore), "_underscore_test");

        assertThrows("MAKE-XML-STRING-PARAMETER-EXCEPTION", \make_xml(), ( ("a": 1, "b": 2)), "top-level multi hash");
        assertThrows("MAKE-XML-STRING-PARAMETER-EXCEPTION", \make_xml(), ( ("a": (1, 2))), "top-level multi list");
    }

    deprecatedXmlTestCase() {
        hash o = Hash;
        hash mo = ("o": o);
        string str = makeXMLString("o", o);
        assertEq(True, mo.o == parseXML(str).o, "first parseXML()");
        str = makeFormattedXMLString("o", o);
        assertEq(True, mo == parseXML(str), "second parseXML()");
        list params = (1, True, "string", NOTHING, o + ("emptylist": (), "emptyhash": hash()));
        str = makeFormattedXMLRPCCallStringArgs("test.method", params);
        hash result = ( "methodName" : "test.method", "params" : params );
        assertEq(result, parseXMLRPCCall(str), "makeXMLRPCCallStringArgs() and parseXMLRPCCall()");
        str = makeFormattedXMLRPCCallStringArgs("test.method", params);
        assertEq(result, parseXMLRPCCall(str), "makeFormattedXMLRPCCallStringArgs() and parseXMLRPCCall()");
        str = makeXMLRPCCallString("test.method", True, o);
        result = ( "methodName" : "test.method","params" : (True, o) );
        assertEq(result, parseXMLRPCCall(str), "makeXMLRPCCallString() and parseXMLRPCCall()");
        str = makeFormattedXMLRPCCallString("test.method", True, o);
        assertEq(result, parseXMLRPCCall(str), "makeFormattedXMLRPCCallString() and parseXMLRPCCall()");

        str = makeXMLRPCResponseString(o);
        assertEq(("params": o), parseXMLRPCResponse(str), "first makeXMLRPCResponse() and parseXMLRPCResponse()");
        str = makeFormattedXMLRPCResponseString(o);
        assertEq(("params": o), parseXMLRPCResponse(str), "first makeFormattedXMLRPCResponse() and parseXMLRPCResponse()");
        str = makeXMLRPCFaultResponseString(100, "error");
        hash fr = ( "fault" : ( "faultCode" : 100, "faultString" : "error" ) );
        assertEq(fr, parseXMLRPCResponse(str), "second makeXMLRPCResponse() and parseXMLRPCResponse()");
        str = makeFormattedXMLRPCFaultResponseString(100, "error");
        assertEq(fr, parseXMLRPCResponse(str), "second makeXMLRPCResponse() and parseXMLRPCResponse()");
        o = ( "xml" : (o + ( "^cdata^" : "this string contains special characters &<> etc" )) );
        assertEq(True, o == parseXML(makeXMLString(o)), "xml serialization with cdata");

        if (Option::HAVE_PARSEXMLWITHSCHEMA) {
            o = ( "ns:TestElement" : ( "^attributes^" : ( "xmlns:ns" : "http://qoretechnologies.com/test/namespace" ), "^value^" : "testing" ) );

            assertEq(o, parseXMLWithSchema(makeXMLString(o), Xsd), "parseXMLWithSchema()");
        }
    }

    fileSaxIteratorTestCase() {
        string fn = sprintf("%s%s%s.xml", tmp_location(), DirSep, get_random_string());
        File f();
        f.open(fn, O_CREAT | O_WRONLY | O_TRUNC);
        f.write(Str);
        on_exit
            unlink(fn);
        FileSaxIterator i(fn, "record");
        assertEq(True, i.next());
        assertEq(Rec, i.getValue());
    }

    xmlCommentTestCase() {
        string xml = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>
<file>
  <!--file-->
  <record id=\"1\">
    <!--rec1-->
    <name><!--test1 comment-->test1</name>
    <!--rec1 c2-->
  </record>
  <record id=\"2\">
    <!--rec2-->
    <name>test2</name>
  </record>
  <!--file 2-->
</file>
";

        hash rec = (
            "file": (
                "^comment^": "file",
                "record": (

                    (
                        "^attributes^": ("id": "1"),
                        "^comment^": "rec1",
                        "name": (
                            "^comment^": "test1 comment",
                            "^value^": "test1",
                        ),
                        "^comment1^": "rec1 c2",
                    ),
                    (
                        "^attributes^": ("id": "2"),
                        "^comment^": "rec2",
                        "name": "test2",
                    )
                ),
                "^comment1^": "file 2",
            )
        );

        assertEq(xml, make_xml(rec, XGF_ADD_FORMATTING), "make_xml()");
        assertEq(rec, parse_xml(xml, XPF_PRESERVE_ORDER | XPF_ADD_COMMENTS), "parse_xml()");
    }

    getXmlValueTestCase() {
        hash rec = (
            "normal": "normal elem",
            "nothing": NOTHING,
            "listOfString": ( "list item1", "list item2"),
            "withAtriibutes": (
                "^attributes^": ("a": "a"),
                "^value^": "value with attr",
            ),
            "withAttributesNothing": (
                "^attributes^": ("a": "a"),
            ),
            "integer": 999,
            "bool": True,
            "multiValue": (
                "^value^": "val1",
                "^comment^": "aaa",
                "^value2^": "val2",
            ),
            "integerValue": ("^value^": 888),
            "listValue": ("^value^": (1,2)),
            "hashValue": ("^value^": ("a":"a")),
        );
        assertEq(rec.normal, get_xml_value(rec.normal), "get_xml_value normal");  # coredump
        assertEq(NOTHING, get_xml_value(rec.nothing), "get_xml_value nothing");
        assertEq(NOTHING, get_xml_value(rec.listOfStrings), "get_xml_value list");
        assertEq(rec.listOfStrings[0], get_xml_value(rec.listOfStrings[0]), "get_xml_value list[0]");
        assertEq(rec.withAttributes."^value^", get_xml_value(rec.withAttributes), "get_xml_value withAttributes");
        assertEq(NOTHING, get_xml_value(rec.withAttributesNothing), "get_xml_value withAttributesNothing");
        assertEq(rec.integer, get_xml_value(rec.integer), "get_xml_value integer");
        assertEq(rec.bool, get_xml_value(rec.bool), "get_xml_value bool");
        assertEq(rec.multiValue."^value^", get_xml_value(rec.multiValue), "get_xml_value multivalue");  # core dump
        assertEq(rec.integerValue."^value^", get_xml_value(rec.integerValue), "get_xml_value integervalue");
        assertEq(NOTHING, get_xml_value(rec.listValue), "get_xml_value listvalue");
        assertEq(NOTHING, get_xml_value(rec.hashValue), "get_xml_value hashvalue");
    }

    make_xmlWithHashOptsGeneralTestCase() {
        # raises exception when called with invalid hash
        # no root element
        assertThrows("MAKE-XML-STRING-PARAMETER-EXCEPTION",
                     \make_xml(), ({}, {}));
        # more root elements
        assertThrows("MAKE-XML-STRING-PARAMETER-EXCEPTION",
                     \make_xml(), ({"root1": "foo", "root2": "bar"}, {}));
    }

    make_xmlWithHashOptsArgumentDocVersionTestCase() {
        # invalid value
        assertThrows("MAKE-XML-OPTS-INVALID",
                     ".*invalid argument: 'docVersion'",
                     \make_xml(), (
                         {"root": "foo"},
                         {"docVersion": False}
                     ));

        # default value
        {
            string expected = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n"
                              "<root>foo</root>\n";
            hash input = {
                "root": "foo",
            };
            hash opts = {
            };
            string xml = make_xml(input, opts);
            assertEq(expected, xml);
        }

        # specific value
        {
            string expected = "<?xml version=\"2.0\" encoding=\"UTF-8\"?>\n"
                              "<root>foo</root>\n";
            hash input = {
                "root": "foo",
            };
            hash opts = {
                "docVersion": "2.0",
            };
            string xml = make_xml(input, opts);
            assertEq(expected, xml);
        }
    }

    make_xmlWithHashOptsArgumentEncodingTestCase() {
        # invalid value
        assertThrows("MAKE-XML-OPTS-INVALID",
                     ".*invalid argument: 'encoding'",
                     \make_xml(), (
                         {"root": "foo"},
                         {"encoding": False}
                     ));

        # default value
        {
            string expected = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n"
                              "<root>foo֍</root>\n";
            hash input = {
                "root": "foo֍",
            };
            hash opts = {
            };
            string xml = make_xml(input, opts);
            assertEq(expected, xml);
        }

        # specific value
        {
            string expected = "<?xml version=\"1.0\" encoding=\"US-ASCII\"?>\n"
                              "<root>foo?</root>\n";
            hash input = {
                "root": "foo֍",
            };
            hash opts = {
                "encoding": "US-ASCII",
            };
            string xml = make_xml(input, opts);
            assertEq(expected, xml);
        }
    }

    make_xmlWithHashOptsArgumentFormatWithWhitespacesTestCase() {
        # invalid value
        assertThrows("MAKE-XML-OPTS-INVALID",
                     ".*invalid argument: 'formatWithWhitespaces'",
                     \make_xml(), (
                         {"root": "foo"},
                         {"formatWithWhitespaces": "No"}
                     ));

        # default value
        {
            string expected = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n"
                              "<root><foo>bar</foo></root>\n";
            hash input = {
                "root": {
                    "foo": "bar",
                },
            };
            hash opts = {
            };
            string xml = make_xml(input, opts);
            assertEq(expected, xml);
        }

        # specific value
        {
            string expected = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n"
                              "<root>\n"
                              "  <foo>bar</foo>\n"
                              "</root>\n";
            hash input = {
                "root": {
                    "foo": "bar",
                },
            };
            hash opts = {
                "formatWithWhitespaces": True,
            };
            string xml = make_xml(input, opts);
            assertEq(expected, xml);
        }
    }

    make_xmlWithHashOptsArgumentUseNumericRefsTestCase() {
        # invalid value
        assertThrows("MAKE-XML-OPTS-INVALID",
                     ".*invalid argument: 'useNumericRefs'",
                     \make_xml(), (
                         {"root": "foo"},
                         {"useNumericRefs": "No"}
                     ));

        # default value
        {
            string expected = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n"
                              "<root>foo֍</root>\n";
            hash input = {
                "root": "foo֍",
            };
            hash opts = {
            };
            string xml = make_xml(input, opts);
            assertEq(expected, xml);
        }

        # specific value
        {
            string expected = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n"
                              "<root>foo&#1421;</root>\n";
            hash input = {
                "root": "foo֍",
            };
            hash opts = {
                "useNumericRefs": True,
            };
            string xml = make_xml(input, opts);
            assertEq(expected, xml);
        }
    }

    make_xmlWithHashOptsArgumentDateFormatTestCase() {
        # invalid value
        assertThrows("MAKE-XML-OPTS-INVALID",
                     ".*invalid argument: 'dateFormat'",
                     \make_xml(), (
                         {"root": "foo"},
                         {"dateFormat": False}
                     ));

        # default value
        {
            date d = now();
            string expected = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n"
                              "<root>" + format_date("YYYYMMDDHHmmSS", d) +
                              "</root>\n";
            hash input = {
                "root": d,
            };
            hash opts = {
            };
            string xml = make_xml(input, opts);
            assertEq(expected, xml);
        }

        # specific value
        {
            date d = now();
            string format = "YYYY-MM-DDTHH:mm:SSZ";
            string expected = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n"
                              "<root>" + format_date(format, d) + "</root>\n";
            hash input = {
                "root": d,
            };
            hash opts = {
                "dateFormat": format,
            };
            string xml = make_xml(input, opts);
            assertEq(expected, xml);
        }
    }

    XmlDocConstructorFromHashTestCase() {
        # @throw XMLDOC-CONSTRUCTOR-ERROR error parsing XML string
        {
            assertThrows("XMLDOC-CONSTRUCTOR-ERROR",
                         sub(){ XmlDoc doc({}); });
        }

        # @throw MAKE-XML-ERROR An error occurred serializing the data
        {
            assertThrows("MAKE-XML-ERROR",
                         sub(){ XmlDoc doc({"":""}); });
        }

        # @throw MAKE-XML-OPTS-INVALID the opts hash passed is not valid
        {
            assertThrows("MAKE-XML-OPTS-INVALID",
                         ".*invalid argument: 'docVersion'",
                         sub(){ XmlDoc doc({}, {"docVersion": False}); });
        }

        # default opts
        {
            date d = now();
            hash input = {
                "root": {
                    "date": d,
                    "utf": "֍"
                }
            };
            XmlDoc doc(input);
            string xml = doc.toString();
            string expected = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n"
                              "<root>"
                              "<date>" + format_date("YYYYMMDDHHmmSS", d) +
                              "</date>"
                              "<utf>֍</utf>"
                              "</root>\n";
            assertEq(expected, xml);

            # dates are formatted as string
            hash expectedHash = input;
            expectedHash.root.date = format_date("YYYYMMDDHHmmSS", d);
            assertEq(expectedHash, doc.toQore());
        }

        # specific opts
        {
            date d = now();
            string format = "YYYY-MM-DD HH:mm:SS";
            hash input = {
                "root": {
                    "date": d,
                    "utf": "֍"
                }
            };
            hash opts = {
                "docVersion": "1.99",
                "encoding": "ascii",
                "useNumericRefs": True,
                "formatWithWhitespaces": True,
                "dateFormat": format,
            };
            XmlDoc doc(input, opts);
            string xml = doc.toString();
            string expected = "<?xml version=\"1.99\" encoding=\"US-ASCII\"?>\n"
                              "<root>\n"
                              "  <date>" + format_date(format, d) + "</date>\n"
                              "  <utf>&#1421;</utf>\n"
                              "</root>\n";
            assertEq(expected, xml);

            # dates are formatted as string
            hash expectedHash = input;
            expectedHash.root.date = format_date(format, d);
            assertEq(expectedHash, doc.toQore());
        }
    }

    XmlDocConstructorFromStringTestCase() {
        # @throw XMLDOC-CONSTRUCTOR-ERROR error parsing XML string
        {
            assertThrows("XMLDOC-CONSTRUCTOR-ERROR",
                         sub(){ XmlDoc doc(""); });
        }

        # valid
        {
            date d = now();
            string format = "YYYY-MM-DD HH:mm:SS";
            string xml = "<?xml version=\"1.99\" encoding=\"US-ASCII\"?>\n"
                         "<root>\n"
                         "  <date>" + format_date(format, d) + "</date>\n"
                         "  <utf>&#1421;</utf>\n"
                         "</root>\n";
            # string must be explicitly converted to matched encoding
            XmlDoc doc(convert_encoding(xml, "us-ascii"));
            assertEq(xml, doc.toString());
        }
    }

    XmlDocValidateSchemaTestCase() {
        string schema =
            '<?xml version="1.0"?>'
            '<xs:schema xmlns:xs="http://www.w3.org/2001/XMLSchema">'
            '<xs:element name="root" type="xs:string"/>'
            '</xs:schema>';

        # parse exception with proper desc
        {
            string invalidSchema =
                '<?xml version="1.0"?>'
                '<xs:schema xmlns:xs="http://www.w3.org/2001/XMLSchema">'
                '<xs:element/> <!-- missing name attr -->'
                '</xs:schema>';
            hash input = { "root": "string" };
            XmlDoc doc(input);
            assertThrows("XSD-SYNTAX-ERROR",
                         "The attribute 'name' is required but missing.",
                         \doc.validateSchema(), invalidSchema);
        }

        # valid document against schema
        {
            hash input = { "root": "string" };
            XmlDoc doc(input);
            # nothrow
            doc.validateSchema(schema);
        }

        # invalid document against schema with proper desc
        {
            hash input = { "neroot": "string" };
            XmlDoc doc(input);
            assertThrows("XSD-ERROR", "Element 'neroot':",
                         \doc.validateSchema(), schema);
        }
    }
}
